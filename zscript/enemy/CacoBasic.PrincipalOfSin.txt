class PrincipalBasics : PathFinder2
{
	Array<Actor> InterestPoint;
	int CurrentPoint, WaitBeforeActive;
	actor DetentionRoomPlayer, DetentionRoomSelf, CacoTeacher;
	
	bool ExecutingInProgress, PunishmentInProgress, PunishmentInTime, DetentionForYou;
	
	// Checks for something rule breaking event, be it Player speed, Bullying, eating, etc..
	virtual void SearchForViolators()
	{
		if (ExecutingInProgress) return;
		
		BlockThingsIterator it = BlockThingsIterator.Create (self, 1024); // 1024
		
		while (it.Next ()) 
		{
			let next = it.Thing;
			
			if (!CheckSight (next, SF_SeePastShootableLines)) // Not in sight
				continue;
			
			if (next && next is 'BasicsPlayer' && !next.bKILLED)
			{
				let Player = BasicsPlayer(next);
				if (Player)
				{
					if (Player.MoveSpeed > Player.RunningLength)
					{
						goal = null;
						target = Player;
						Player.A_StartSound("principal/norunninginthehalls", CHAN_AUTO);
						ExecutingInProgress = true;
						
					}
				}
			}
		}
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		WaitBeforeActive = level.time + 35 * 2;
	}
	
	override void Tick()
	{
		Super.Tick();
		
		// PoS will randomly wander around the map if he's activated.
		if (!target && level.time > WaitBeforeActive)
		{
			int rnd = random(0,InterestPoint.Size()-1);
			if (InterestPoint[rnd])
				target = InterestPoint[rnd];
			
			//target = InterestPoint[random(0,InterestPoint.Size())];
		}
		
		// Player broke the rule. Let's punish him
		// First we need to get closer to him, and be our target
		// If we catched them, teleport him and us in the Detention Room via actor pointers
		if (ExecutingInProgress && target is 'BasicsPlayer')
		{
			if (target && Distance2D(target) < 100 && !PunishmentInProgress)
			{
				if (target.bKILLED)
				{
					ExecutingInProgress = false;
					target = null;
					goal = null;
					return;
				}
				
				target.SetOrigin(DetentionRoomPlayer.pos, TRUE);
				target.Angle = DetentionRoomPlayer.angle;
				SetOrigin(DetentionRoomSelf.pos, TRUE);
				PunishmentInProgress = true;
			}
			
			if (PunishmentInProgress)
			{
				target.A_SetSpeed(0);
				// Let's say the detention time
				if (!PunishmentInTime)
				{
					A_StartSound("principal/15", CHAN_VOICE);
					CacoTeacher.goal = null;
					CacoTeacher.target = target;
					PunishmentInTime = true;
				}
				
				// "... Detention for you!"
				if (!(IsActorPlayingSound(CHAN_VOICE)) && PunishmentInTime && !DetentionForYou)
				{
					A_StartSound("principal/detentionforyou", CHAN_VOICE);
					DetentionForYou = true;
				}
				
				// And let's doodle away by giving us a random interest point.
				if (!(IsActorPlayingSound(CHAN_VOICE)) && PunishmentInTime && DetentionForYou)
				{
					// Give the Player's speed back
					target.A_SetSpeed(1);
					target = null;
					goal = null;
					PunishmentInProgress = false;
					PunishmentInTime = false;
					DetentionForYou = false;
					ExecutingInProgress = false;
					//target = InterestPoint[random(0,InterestPoint.Size())];
					//goal = InterestPoint[random(0,InterestPoint.Size())];
					
					int rnd = random(0,InterestPoint.Size()-1);
					if (InterestPoint[rnd])
					{
						target = InterestPoint[rnd];
						goal = InterestPoint[rnd];
					}
				}
			
			}
		
		}
		
		if (ExecutingInProgress) return;
		
		if ((target && Distance2D(target) < 100 || target is 'PlayerPawn') && !ExecutingInProgress)
		{
			target = null;
			goal = null;
			
			int rnd = random(0,InterestPoint.Size()-1);
			if (InterestPoint[rnd])
				target = InterestPoint[rnd];
		}
	
	}
	
	Default
	{
		Health 250;
		Mass 10000000;
		PainChance 255;
		+NOICEDEATH
		+OLDRADIUSDMG
		Speed 5;
		PainSound "brain/pain";
		DeathSound "brain/death";
		-SOLID;
		Monster;
		Scale 0.8;
	}
	States
	{
	Spawn:
		BBRN A 1;
		Goto See;
	See:
		BBRN A 1 { if (target && !invoker.PunishmentInProgress) A_Star(); }
		BBRN A 0 SearchForViolators();
		Loop;
	Pain:
		BBRN B 36 A_BrainPain;
		Goto Spawn;
	Death:
		BBRN A 100;
		BBRN AA 10;
		BBRN A -1;
		Stop;
	}
}

// Goes to these points chosen randomly when wandering and is not after the Player
class PrincipalInterest : Actor
{
    Default
    {
        Radius 16;
        Height 16;
		Scale 0.3;

        RenderStyle "Add";

        +NOGRAVITY
        +NOBLOCKMAP
    }

    States
    {
    Spawn:
        BBRN A 6 Bright
        {
            angle += 10;
        }
        Loop;
    }
}

class DetentionRoomPlayer : Actor
{
	Default
    {
        Radius 16;
        Height 16;
		Scale 0.3;

        +NOGRAVITY
        +NOBLOCKMAP
    }

    States
    {
    Spawn:
        BBRN A 6;
        Loop;
    }
}

class DetentionRoomPrincipal : Actor
{
	Default
    {
        Radius 16;
        Height 16;
		Scale 0.3;

        +NOGRAVITY
        +NOBLOCKMAP
    }

    States
    {
    Spawn:
        BBRN B 6 Bright
        {
            angle += 10;
        }
        Loop;
    }
}