class DootBasics : BasicsActor
{
	Array<Actor> InterestPoint;
	bool DootingInProgress, CopiedPoints;
	bool HearTheDoot;
	
	int TimeBeforeTarget;
	
	// We seek Players to doot at. Doot doot
	virtual void SearchForDootable()
	{
		if (DootingInProgress || level.time < TimeBeforeTarget) return;
		
		BlockThingsIterator it = BlockThingsIterator.Create (self, 1024); // 1024
		
		while (it.Next ()) 
		{
			let next = it.Thing;
			
			if (!CheckSight (next, SF_SeePastShootableLines)) // Not in sight
				continue;
			
			if (next && next is 'BasicsPlayer' && !next.bKILLED)
			{
				let Player = BasicsPlayer(next);
				if (Player)
				{
					goal = null;
					target = null;
					target = Player;
					Player.A_StartSound("mrdoot/horn", CHAN_BODY);
					DootingInProgress = true;
					path.Clear();
				}
			}
		}
	}
	
	override void Tick()
	{
		Super.Tick();
		
		if (!target && CopiedPoints)
		{
			int rnd = random(0,InterestPoint.Size()-1);
			if (InterestPoint[rnd])
				target = InterestPoint[rnd];
		}
		
		if (target is 'BasicsPlayer')
		{
			if (target.bKILLED)
			{
				target = null;
				goal = null;
				DootingInProgress = false;
			}
			
			if (DootingInProgress && target && Distance2D(target) < 120)
			{
				if (!HearTheDoot)
				{
					// Pick a random doot we will doot
					int randomnumber = random[pickline](1,7);
					sound doot = string.Format("mrdoot/doot%i",randomnumber);
					A_StartSound(doot, CHAN_VOICE, CHANF_NOSTOP);
					target.A_Face(self);
					HearTheDoot = true;
				}
				if (HearTheDoot)
				{
					A_SetSpeed(0);
					target.A_SetSpeed(0);
					if (random[yes]() > 40 && level.time % 10)
						angle = random(-90,90);
					
					target.A_SetSpeed(0);
					if (!(IsActorPlayingSound(CHAN_VOICE)))
					{
						TimeBeforeTarget = level.time + 35 * 7;
						A_StartSound("mrdoot/horn");
						HearTheDoot = false;
						target.A_SetSpeed(1);
						DootingInProgress = false;
						A_SetSpeed(10);
					}
				}
			}
		}
		
		if (DootingInProgress) return;
		
		if ((target && Distance2D(target) < 100 || target is 'PlayerPawn') && !DootingInProgress && CopiedPoints)
		{
			target = null;
			goal = null;
			
			int rnd = random(0,InterestPoint.Size()-1);
			if (InterestPoint[rnd])
				target = InterestPoint[rnd];
		}
	
	}
	
	Default
	{
		Health 300;
		Radius 10;
		Height 10;
		Mass 500;
		Speed 10;
		PainChance 100;
		Monster;
		MeleeThreshold 196;
		+MISSILEMORE 
		+FLOORCLIP
		-SOLID
		SeeSound "";
		PainSound "";
		DeathSound "";
		ActiveSound "";
		MeleeSound "";
		HitObituary "$OB_UNDEADHIT";
		Obituary "$OB_UNDEAD";
		Tag "$FN_REVEN";
	}
	States
	{
	Spawn:
		SKEL AB 10;
		Goto See;
	See:
		SKEL AABBCCDDEEFF 2 { if (!HearTheDoot) A_Star(); }
		SKEL A 0 SearchForDootable();
		Loop;
	Pain:
		SKEL L 5;
		SKEL L 5 A_Pain;
		Goto See;
	Death:
		SKEL LM 7;
		SKEL N 7 A_Scream;
		SKEL O 7 A_NoBlocking;
		SKEL P 7;
		SKEL Q -1;
		Stop;
	Raise:
		SKEL Q 5;
		SKEL PONML 5;
		Goto See;
	}
}