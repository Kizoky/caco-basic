class BasicsPlayer : PlayerPawn 
{
	// I don't want to touch Doom's movement code as I don't know fully well how it works
	int RunningStopCooldown;
	bool RunningStop;
	
	double MoveSpeed, RestPoints, RunningLength;
	int BooksDone;
	
	bool DeveloperCheating;
	
	virtual void FinishedBook()
	{
		BooksDone += 1;
		A_StartSound("caco/hiss", CHAN_VOICE);
		A_Quake(4,80,0,1000);
	}
	
	override void Tick()
	{
		Super.Tick();
		if ( !player || (player.mo != self) ) return;
		
		// MoveSpeed > 1.62 = running
		MoveSpeed = vel.xy.Length();
		
		if (!DeveloperCheating)
		{
			if (MoveSpeed == 0 && RestPoints <= 100)
				RestPoints += 0.3;
			else if (MoveSpeed > RunningLength && RestPoints >= 0)
				RestPoints -= 0.3;
		}
		
		if (RestPoints <= 0)
		{
			A_SetSpeed(0.3);
		}
		else
			A_SetSpeed(1);
		
		// Disables Doom's falling bob
		// Although I don't think we'll need this
		player.deltaviewheight = ViewHeight;
		
		// There's a slight delay before completely stopping the Player
		UserCmd cmd = player.cmd;
		if (!(GetPlayerInput(INPUT_FORWARDMOVE)) && !(GetPlayerInput(INPUT_SIDEMOVE)) && !(player.cmd.buttons & BT_JUMP) && player.onground)
		{	
			if (level.time > RunningStopCooldown)
			{
				vel.xy = (0,0);
				RunningStop = false;
			}
		}
		else
		{
			if (!RunningStop)
			{
				RunningStopCooldown = level.time + 8;
				RunningStop = true;
			}
		}
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		RestPoints = 100;
		
		// Higher than this means Player is running
		RunningLength = 1.62;
		
		BooksDone = 1;
		
		cvar developercvar = cvar.findcvar('basics_developer');
		bool developer = (developercvar != null && developercvar.GetBool ());
		
		if (developer && !Multiplayer)
		{
			Console.Printf("Developer mode enabled.");
			DeveloperCheating = true;
			RestPoints = 666;
		}
	}

	Default
	{
		Player.DisplayName "The Student";
		Speed 1;
		Health 100;
		Radius 16;
		Height 56;
		Mass 100;
		PainChance 255;
		Player.ViewBob 0;
		Player.ForwardMove 0.20, 0.30; //(walk, run)
		Player.SideMove 0.08, 0.22; //(walk, run)
		Player.JumpZ 0;
		Player.ViewHeight 47;
		Player.StartItem "HUDViewable", 1;
		+SOLID
	}
	States
	{
	Spawn:
		PLAY A -1;
		Loop;
	See:
		PLAY ABCD 4;
		Loop;
	Missile:
		PLAY E 12;
		Goto Spawn;
	Melee:
		PLAY F 6 BRIGHT;
		Goto Missile;
	Pain:
		PLAY G 4;
		PLAY G 4 A_Pain;
		Goto Spawn;
	Death:
		PLAY H 0 A_PlayerSkinCheck("AltSkinDeath");
	Death1:
		PLAY H 10;
		PLAY I 10 A_PlayerScream;
		PLAY J 10 A_NoBlocking;
		PLAY KLM 10;
		PLAY N -1;
		Stop;
	XDeath:
		PLAY O 0 A_PlayerSkinCheck("AltSkinXDeath");
	XDeath1:
		PLAY O 5;
		PLAY P 5 A_XScream;
		PLAY Q 5 A_NoBlocking;
		PLAY RSTUV 5;
		PLAY W -1;
		Stop;
	AltSkinDeath:
		PLAY H 6;
		PLAY I 6 A_PlayerScream;
		PLAY JK 6;
		PLAY L 6 A_NoBlocking;
		PLAY MNO 6;
		PLAY P -1;
		Stop;
	AltSkinXDeath:
		PLAY Q 5 A_PlayerScream;
		PLAY R 0 A_NoBlocking;
		PLAY R 5 A_SkullPop;
		PLAY STUVWX 5;
		PLAY Y -1;
		Stop;
	}
}